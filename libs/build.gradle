//TODO: use directly when JPMS will be impl
buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath 'org.gradlex:extra-java-module-info:1.4'
	}
}

//TODO: https://github.com/spullara/mustache.java/issues/289
project(':libs:mustache') {
	apply plugin: 'java'
	apply plugin: 'org.gradlex.extra-java-module-info'

	configurations {
		moduleJar {
			canBeConsumed = true
			canBeResolved = false
		}
	}

	version mustacheVersion

	dependencies {
		implementation group: 'com.github.spullara.mustache.java', name: 'compiler', version: mustacheVersion
	}

	extraJavaModuleInfo {
		failOnMissingModuleInfo = true
		module('com.github.spullara.mustache.java:compiler', 'com.github.mustachejava') {
			requiresStatic 'java.logging'

			exports 'com.github.mustachejava'
			exports 'com.github.mustachejava.reflect'
			exports 'com.github.mustachejava.util'
		}
	}
	artifacts {
		moduleJar configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts
			.find({ it.moduleVersion.id.name == 'compiler' })
			.file
	}

}

//TODO: https://github.com/commonmark/commonmark-java/issues/125
project(':libs:commonmark') {
	apply plugin: 'java'
	apply plugin: 'org.gradlex.extra-java-module-info'

	configurations {
		moduleJar {
			canBeConsumed = true
			canBeResolved = false
		}
	}
	version commonmarkVersion

	dependencies {
		implementation platform(group: 'org.commonmark', name: 'commonmark-parent', version: commonmarkVersion)
		implementation group: 'org.commonmark', name: 'commonmark'
		implementation group: 'org.commonmark', name: 'commonmark-ext-yaml-front-matter'
		implementation group: 'org.commonmark', name: 'commonmark-ext-image-attributes'
		implementation group: 'org.commonmark', name: 'commonmark-ext-gfm-tables'
	}


	extraJavaModuleInfo {
		failOnMissingModuleInfo = true
		module('org.commonmark:commonmark', 'commonmark') {
			mergeJar('org.commonmark:commonmark-ext-yaml-front-matter')
			mergeJar('org.commonmark:commonmark-ext-image-attributes')
			mergeJar('org.commonmark:commonmark-ext-gfm-tables')

			exports 'org.commonmark.node'
			exports 'org.commonmark.parser'
			exports 'org.commonmark.renderer'
			exports 'org.commonmark.renderer.html'
			exports 'org.commonmark.ext.front.matter'
			exports 'org.commonmark.ext.image.attributes'
			exports 'org.commonmark.ext.gfm.tables'
		}
	}

	artifacts {
		moduleJar configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts
			.find({ it.moduleVersion.id.name == 'commonmark' })
			.file
	}

}

//TODO: https://github.com/haraldk/TwelveMonkeys/issues/292
project(':libs:twelvemonkeys') {
	apply plugin: 'java'
	apply plugin: 'org.gradlex.extra-java-module-info'

	configurations {
		moduleJar {
			canBeConsumed = true
			canBeResolved = false
		}
	}
	version twelvemonkeysVersion

	dependencies {
		implementation platform(group: 'com.twelvemonkeys.bom', name: 'bom', version: twelvemonkeysVersion)
		implementation group: 'com.twelvemonkeys.imageio', name: 'imageio-jpeg'
		implementation group: 'com.twelvemonkeys.imageio', name: 'imageio-webp'
		implementation group: 'com.twelvemonkeys.imageio', name: 'imageio-tiff'
	}

	extraJavaModuleInfo {
		failOnMissingModuleInfo = true
		module('com.twelvemonkeys.imageio:imageio-core', 'com.twelvemonkeys.imageio') {
			mergeJar 'com.twelvemonkeys.common:common-image'
			mergeJar 'com.twelvemonkeys.common:common-io'
			mergeJar 'com.twelvemonkeys.common:common-lang'
			mergeJar 'com.twelvemonkeys.imageio:imageio-metadata'
			mergeJar 'com.twelvemonkeys.imageio:imageio-jpeg'
			mergeJar 'com.twelvemonkeys.imageio:imageio-webp'
			mergeJar 'com.twelvemonkeys.imageio:imageio-tiff'

			requiresStatic 'java.desktop'

			exports 'com.twelvemonkeys.imageio.stream'
			exports 'com.twelvemonkeys.imageio.color'
			exports 'com.twelvemonkeys.imageio.plugins.jpeg'
			exports 'com.twelvemonkeys.imageio.plugins.webp'
			exports 'com.twelvemonkeys.imageio.plugins.tiff'
		}
	}

	artifacts {
		moduleJar configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts
			.find({ it.moduleVersion.id.name == 'imageio-core' })
			.file
	}

}


project(':libs:jai') {
	apply plugin: 'java'
	apply plugin: 'org.gradlex.extra-java-module-info'

	configurations {
		moduleJar {
			canBeConsumed = true
			canBeResolved = false
		}
	}
	version jaiVersion

	dependencies {
		implementation group: 'com.github.jai-imageio', name: 'jai-imageio-core', version: jaiVersion
	}

	extraJavaModuleInfo {
		failOnMissingModuleInfo = true
		module('com.github.jai-imageio:jai-imageio-core', 'com.github.jai_imageio') {
			requiresStatic 'java.desktop'

			exports 'com.github.jaiimageio.impl.stream'
			exports 'com.github.jaiimageio.impl.plugins.wbmp'
			exports 'com.github.jaiimageio.impl.plugins.bmp'
			exports 'com.github.jaiimageio.impl.plugins.gif'
			exports 'com.github.jaiimageio.impl.plugins.pcx'
			exports 'com.github.jaiimageio.impl.plugins.pnm'
			exports 'com.github.jaiimageio.impl.plugins.raw'

			//use from twelvemonkeys
			ignoreServiceProvider 'com.github.jaiimageio.impl.plugins.tiff.TIFFImageReaderSpi'
			ignoreServiceProvider 'com.github.jaiimageio.impl.plugins.tiff.TIFFImageWriterSpi'
		}
	}

	artifacts {
		moduleJar configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts
			.find({ it.moduleVersion.id.name == 'jai-imageio-core' })
			.file
	}

}
