package net.pawet.pawgen

import spock.lang.Specification
import spock.lang.Unroll
import util.ImageUtil
import util.PawgenFs

import java.lang.Void as Should
import java.nio.file.Path

import static java.nio.file.Files.*
import static java.time.Instant.EPOCH
import static util.ImageUtil.createTestImageAsByte

class ApplicationSpec extends Specification {

	@Unroll
	Should 'build site #fileSystemProvider.method'() {
		given: 'filesystem'
		PawgenFs pawFs = fileSystemProvider.call()
		Path outputDir = pawFs.dir('out')
		Path templateDir = pawFs.dir('templates')
		Path contentDir = pawFs.dir('contentDir/data')
		Path filesDir = pawFs.dir('contentDir/files')
		Path staticDir = pawFs.dir('contentDir/static')
		and: 'pawgen config'
		def configFile = pawFs.writeProperties('config.properties', [
			contentDir      : contentDir.toUri() as String,
			staticDirs      : "${filesDir.toUri()},${staticDir.toUri()}**" as String,
			templatesDir    : templateDir.toUri() as String,
			outputDir       : outputDir.toUri() as String,
			hosts           : 'localhost',
			dateFrom        : EPOCH.toString(),
			deployers       : 'NONE',
			'watermark.text': '', // disable as watermarking produces different result on win, it makes assertion not trivial
		]).toUri() as String
		and: 'site data'
		write(pawFs.dir("$contentDir/newcat/test/_img").resolve('toster.bmp'), createTestImageAsByte(251, 27))

		def newcatTestArticle = contentDir.resolve('newcat/test').resolve('index.by.md')
		writeString(newcatTestArticle, '''\
---
title: illegal chars: *?|
type: article
---
  <a href="http://example.com">external link</a>
  <a href="example">internal link</a>
  <a href="example/illegal chars: *?|">bad internal link</a>
  <a href="http://localhost">internal link with host</a>
  ![alt text](_img/toster.bmp)
''')
		def newcatArticle = contentDir.resolve('newcat').resolve('index.en.md')
		writeString(newcatArticle, '''\
---
title: newcat
type: article
---
newcat
''')
		var imageBmp = createTestImageAsByte(46, 27)
		write(contentDir.resolve('image.bmp'), imageBmp)
		writeString(contentDir.resolve('index.en.md'), '''\
---
title: Main
type: article
file: staticFile.bin
aliases:
  - /alias.html
---
[testArticle](/files/staticFile.bin) ![alt text](image.bmp)
''')
		writeString(staticDir.resolve('test.css'), 'test')
		write(filesDir.resolve('staticFile.bin'), 'test'.bytes)
		writeString(templateDir.resolve('index.html.mustache'), '''
Parent: {{parent.title}} "{{#func.relativize}}{{{parent.url}}}{{/func.relativize}}"
{{#children}}Child: {{title}} "{{#../func.relativize}}{{{url}}}{{/../func.relativize}}"\n{{/children}}
Content:
{{{.}}}
''')
		when:
		def result = Application.run([configFile])
		then:
		result == 0
		and:
		pawFs.listFiles(outputDir) == [
			'files/staticFile.bin',
			'main.html',
			'test.css',
			'_redirects',
			'newcat/newcat.html',
			'newcat/test/illegal_chars_____.html',
			'newcat/test/_img/toster.bmp',
		].collect(outputDir.&resolve) as Set
		and:
		verifyAll {
			readString(outputDir.resolve('main.html')) == """
Parent:  ""
Child: newcat "newcat/newcat.html"
Content:
<p><a href="/files/staticFile.bin">testArticle</a> <img src="data:image/bmp;base64,${imageBmp.encodeBase64()}" alt="alt text" width="46" class="img_left" height="27" /></p>

"""
			readString(outputDir.resolve('_redirects')) == """\
#Autogenerated redirectsFile
/alias.html /main.html

"""
			readString(outputDir.resolve('newcat/newcat.html')) == """
Parent: Main "../main.html"
Child: illegal chars: *?| "test/illegal_chars_____.html"
Content:
<p>newcat</p>

"""
			readString(outputDir.resolve('newcat/test/illegal_chars_____.html')) == """
Parent: newcat "../newcat.html"
Content:
<p><a href="http://example.com">external link</a>
<a href="example">internal link</a>
<a href="example/illegal chars: *?|">bad internal link</a>
<a href="http://localhost">internal link with host</a>
<img src="data:image/jpg;base64,/9j/4AAQSkZJRgABAgAAAQABAAD//gAIcGF3Z2Vu/9sAQwAKBwcIBwYKCAgICwoKCw4YEA4NDQ4dFRYRGCMfJSQiHyIhJis3LyYpNCkhIjBBMTQ5Oz4+PiUuRElDPEg3PT47/9sAQwEKCwsODQ4cEBAcOygiKDs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7Ozs7/8AAEQgAGwD6AwEiAAIRAQMRAf/EAB8AAAEFAQEBAQEBAAAAAAAAAAABAgMEBQYHCAkKC//EALUQAAIBAwMCBAMFBQQEAAABfQECAwAEEQUSITFBBhNRYQcicRQygZGhCCNCscEVUtHwJDNicoIJChYXGBkaJSYnKCkqNDU2Nzg5OkNERUZHSElKU1RVVldYWVpjZGVmZ2hpanN0dXZ3eHl6g4SFhoeIiYqSk5SVlpeYmZqio6Slpqeoqaqys7S1tre4ubrCw8TFxsfIycrS09TV1tfY2drh4uPk5ebn6Onq8fLz9PX29/j5+v/EAB8BAAMBAQEBAQEBAQEAAAAAAAABAgMEBQYHCAkKC//EALURAAIBAgQEAwQHBQQEAAECdwABAgMRBAUhMQYSQVEHYXETIjKBCBRCkaGxwQkjM1LwFWJy0QoWJDThJfEXGBkaJicoKSo1Njc4OTpDREVGR0hJSlNUVVZXWFlaY2RlZmdoaWpzdHV2d3h5eoKDhIWGh4iJipKTlJWWl5iZmqKjpKWmp6ipqrKztLW2t7i5usLDxMXGx8jJytLT1NXW19jZ2uLj5OXm5+jp6vLz9PX29/j5+v/aAAwDAQACEQMRAD8A8ZooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKACiiigAooooAKKKKAP//Z" alt="alt text." onClick="showLightbox(this, '_img/toster.bmp')" width="250" title="alt text." class="g_img" height="27" /></p>

"""
		}
		and:
		pawFs.readAttributes(outputDir, 'sha1') == [
			(outputDir.resolve('newcat/test/_img/toster.bmp'))        : 'd11ee4e14abb11b2efc49e6a4e98350ec6d036be',
			(outputDir.resolve('newcat/test/illegal_chars_____.html')): 'cdb0fa4ff5e3a749fef028047cdff5fccd522edf',
			(outputDir.resolve('newcat/newcat.html'))                 : 'c0de7b5df1243a40ff30128e50401da131350439',
			(outputDir.resolve('files/staticFile.bin'))               : 'a94a8fe5ccb19ba61c4c0873d391e987982fbbd3',
			(outputDir.resolve('test.css'))                           : 'a94a8fe5ccb19ba61c4c0873d391e987982fbbd3',
			(outputDir.resolve('main.html'))                          : 'a1baac9a077a77388fb1a94132a2c7f968fe73fc',
			(outputDir.resolve('_redirects'))                         : 'c6078c5813487373e08a746e1568d448fb47d57d',
		]
		cleanup:
		pawFs.close()
		where:
		fileSystemProvider << [
			PawgenFs::unixWithUserAttrs,
			PawgenFs::unix,
			PawgenFs::win,
			PawgenFs::osx,
//			PawgenFs::tmpZipFs, //there are races so file can be created twice
			PawgenFs::tmpFs,
		]
	}

}
