plugins {
	id 'java'
	id 'org.gradlex.extra-java-module-info' version '1.4'
}

version flexmarkVersion

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(jdkVersion))
	}
}

dependencies {
	implementation group: 'com.vladsch.flexmark', name: 'flexmark', version: flexmarkVersion
	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-yaml-front-matter', version: flexmarkVersion
//	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-tables', version: flexmarkVersion
//	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-autolink', version: flexmarkVersion
//	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-anchorlink', version: flexmarkVersion
//	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-typographic', version: flexmarkVersion
//	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-footnotes', version: flexmarkVersion
	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-attributes', version: flexmarkVersion
//	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-enumerated-reference', version: flexmarkVersion
//	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-aside', version: flexmarkVersion
//	implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-toc', version: flexmarkVersion
}


extraJavaModuleInfo {
	failOnMissingModuleInfo = true
	module('com.vladsch.flexmark:flexmark', 'flexmark') {
		mergeJar('com.vladsch.flexmark:flexmark-util')
		mergeJar('com.vladsch.flexmark:flexmark-util-format')
		mergeJar('com.vladsch.flexmark:flexmark-util-ast')
		mergeJar('com.vladsch.flexmark:flexmark-util-builder')
		mergeJar('com.vladsch.flexmark:flexmark-util-dependency')
		mergeJar('com.vladsch.flexmark:flexmark-util-html')
		mergeJar('com.vladsch.flexmark:flexmark-util-options')
		mergeJar('com.vladsch.flexmark:flexmark-util-sequence')
		mergeJar('com.vladsch.flexmark:flexmark-util-collection')
		mergeJar('com.vladsch.flexmark:flexmark-util-data')
		mergeJar('com.vladsch.flexmark:flexmark-util-misc')
		mergeJar('com.vladsch.flexmark:flexmark-util-visitor')
		mergeJar('com.vladsch.flexmark:flexmark-ext-yaml-front-matter')
		mergeJar('com.vladsch.flexmark:flexmark-ext-attributes')

		exports 'com.vladsch.flexmark.ext.attributes'
		exports 'com.vladsch.flexmark.ext.yaml.front.matter'
		exports 'com.vladsch.flexmark.html'
		exports 'com.vladsch.flexmark.parser'
		exports 'com.vladsch.flexmark.util.misc'
	}
}

configurations {
	moduleJar {
		canBeConsumed = true
		canBeResolved = false
	}
}

artifacts {
	moduleJar configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts
		.find({ it.moduleVersion.id.name == 'flexmark' })
		.file
}
